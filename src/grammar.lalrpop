use crate::ast::{ParsedExpr, Literal, Id};
use crate::lexer::{LexicalError, Token};
use crate::parser::grammar_support;
use crate::types::{Type, TyVar, TyCon, Kind, QualType, QualPred, Qual, Pred, Scheme};

grammar;

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token {
        "(" => Token::LParens,
        ")" => Token::RParens,
        "let" => Token::Let,
        "in" => Token::In,
        "=" => Token::Equals,
        ":" => Token::Colon,
        "," => Token::Comma,
        "." => Token::Dot,
        "->" => Token::Arrow,
        "=>" => Token::FatArrow,
        "fun" => Token::Fun,
        "integer literal" => Token::IntegerLiteral(<i64>),
        "string literal" => Token::StringLiteral(<String>),
        "camel case identifier" => Token::CamelCaseIdentifier(<String>),
        "pascal case identifier" => Token::PascalCaseIdentifier(<String>),
    }
}

// utility for comma-separated lists
CommaSeparated<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

pub Expr: Box<ParsedExpr> = {
    SubExpr,
    LambdaExpr,
    AppExpr,
    LetExpr,
}

SubExpr: Box<ParsedExpr> = {
    Terminal,
    "(" <e:Expr> ")" => e
}

Terminal: Box<ParsedExpr> = {
    <int:"integer literal"> => {
        Box::new(ParsedExpr::Lit(Literal::Int(int)))
    },
    <str:"string literal"> => {
        Box::new(ParsedExpr::Lit(Literal::Str(str)))
    },
    <id:"camel case identifier"> => {
        Box::new(ParsedExpr::Var(Id::new(&id)))
    },
}

LetExpr: Box<ParsedExpr> = {
    "let" <id:"camel case identifier"> "=" <e1:Expr> "in" <e2:Expr> => {
        Box::new(ParsedExpr::Let(Id::new(&id), e1, e2))
    }
}

Args: Vec<Id> = {
    <params:"camel case identifier"*> => {
        params.into_iter().map(|param| Id::new(&param)).collect::<Vec<_>>()
    }
}

LambdaExpr: Box<ParsedExpr> = {
    "fun" <args:Args> "->" <body:Expr> => {
        Box::new(ParsedExpr::Lambda(args, body))
    }
}

AppExpr: Box<ParsedExpr> = {
    <e:SubExpr> <es:SubExpr+> => {
        Box::new(ParsedExpr::App(e, es))
    }
}

pub TypeExpr: Type = {
    TypeTerminal,
    TypeArrow,
    TypeApp
}

// TODO: by default both of them have Kind::Star, not sure if it's ok or not
TypeTerminal: Type = {
    <id:"camel case identifier"> => {
        Type::Var(TyVar(Id::new(&id), Kind::Star))
    },
    <id:"pascal case identifier"> => {
        Type::Con(TyCon(Id::new(&id), Kind::Star))
    },
}

TypeArrow: Type = {
    <t1:TypeTerminal> "->" <t2:TypeTerminal> => {
        Type::Arrow(Box::new(t1), Box::new(t2))
    }
}

// TODO: for now we only support applications of kind * -> *
// TODO: workaround, improve this
TypeApp: Type = {
    <t1:"camel case identifier"> <t2: "camel case identifier"> => {
        grammar_support::make_type_application(&t1, &t2)
    },
    <t1:"camel case identifier"> <t2: "pascal case identifier"> => {
        grammar_support::make_type_application(&t1, &t2)
    },
    <t1:"pascal case identifier"> <t2: "pascal case identifier"> => {
        grammar_support::make_type_application(&t1, &t2)
    },
    <t1:"pascal case identifier"> <t2: "camel case identifier"> => {
        grammar_support::make_type_application(&t1, &t2)
    },
}

// TODO: for now we don't support syntax like `Show a => a -> a`, only `(Show a) => a -> a`
pub QualTypeExpr: QualType = {
    "(" <preds:Predicates> ")" "=>" <ty:TypeExpr> => {
        Qual::new(preds, ty)
    },
    <ty:TypeExpr> => {
        Qual::new(vec![], ty)
    }
}

pub QualPredExpr: QualPred = {
    "(" <preds:Predicates> ")" "=>" <pred:Predicate> => {
        Qual::new(preds, pred)
    },
    <pred:Predicate> => {
        Qual::new(vec![], pred)
    }
}

Predicates = CommaSeparated<Predicate>;

Predicate: Pred = {
    <type_class:"pascal case identifier"> <ty:TypeExpr> => {
        Pred::new(Id::new(&type_class), ty)
    }
}

pub TypeScheme: Scheme = {
    <vars:QuantifiedVariables> "." <qt:QualTypeExpr> => {
        Scheme(vars, qt)
    },
    <qt:QualTypeExpr> => {
        Scheme(vec![], qt)
    },
}

QuantifiedVariables = CommaSeparated<TypeVariable>;

TypeVariable: TyVar = {
    // TODO: for nor all type variables have kind *
    <tyvar:"camel case identifier"> => {
        TyVar(Id::new(&tyvar), Kind::Star)
    }
}
